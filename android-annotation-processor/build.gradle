apply plugin: 'java-library'
apply plugin: 'kotlin'

apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

group = 'name.wildswift.android'
version = '0.1'

dependencies {
    implementation 'name.wildswift.android:android-annotations:0.1'

    implementation "org.jetbrains.kotlinx:kotlinx-metadata-jvm:$kotlinx_metadata_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.squareup:kotlinpoet:0.7.0'
}

sourceCompatibility = "1.7"
targetCompatibility = "1.7"

publishing {
    publications {
        Production(MavenPublication) {
            artifact("$buildDir/libs/android-annotation-processor-${this.version}.jar")
            groupId this.group
            artifactId 'android-annotation-processor'
            version this.version

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    // Ensure dependencies such as fileTree are not included in the pom.
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publications = ['Production']
    pkg {
        repo = 'general'
        name = 'android-annotation-processor'
        licenses = ['Apache-2.0']
        publish = true
        publicDownloadNumbers = true
        vcsUrl = 'https://github.com/wild-swift/ws-android-annotations.git'
        version {
            name = '0.1'
            desc = 'First alpha version'
            released  = new Date()
            vcsTag = '0.1'
        }
    }
}